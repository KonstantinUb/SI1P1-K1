; Univerzitet u Beogradu, Elektrotehnicki fakultet
; Odsek za softversko inzenjerstvo
; Programiranje 1 (13S111P1)
;
; Prvi kolokvijum 2015/2016
; Zadatak
;
; github.com/vomindoraan, 2016

        N = 1
        A = 2
        B = 3
        C = 4
        END = 5
        J = 6
        T = 7
        AJ = 0          ; Ista lokacija, vise imena
        BJ = 0
        CJ = 0

        A_START = 100   ; Pocetne adr. nizova (razmaci od 100 su bezbedni)
        B_START = 200
        C_START = 300

        ORG 8

        MOV A, #A_START
        MOV B, #B_START
        MOV C, #C_START

        IN N
        IN (A), N
        IN (B), N
        IN (C), N

        ADD END, A, N
        MOV J, 0        ; J je indeks koji pomaze pri izbacivanju iz nizova

petlja: BEQ A, END, kraj    ; Izlaz iz petlje, A je doslo do kraja niza
        JSR trougao
        JSR izbaci
        ADD A, A, 1
        ADD B, B, 1
        ADD C, C, 1
        BEQ N, N, petlja    ; Bezuslovni skok

kraj:   MOV N, J            ; Nizovi su skraceni, u J se nalazi nova duzina

        MOV A, #A_START     ; Vraca adrese na pocetke nizova
        MOV B, #B_START
        MOV C, #C_START
        OUT (A), N
        OUT (B), N
        OUT (C), N

        STOP

    ;;; Potprogrami ;;;

trougao:ADD T, (A), (B)     ; T je zbir dveju stranica
        BGT (C), T, nije    ; Ako zbir dve nije veci od trece, nije trougao
        BEQ (C), T, nije    ; Suprotan uslov od T > (C) je (C) >= T
        ADD T, (B), (C)
        BGT (A), T, nije
        BEQ (A), T, nije
        ADD T, (A), (C)
        BGT (B), T, nije
        BEQ (B), T, nije

        MOV T, 1            ; U T se sad cuva da li je trougao
        RTS
nije:   MOV T, 0
        RTS

izbaci: ADD AJ, #A_START, J ; Racuna se adresa J. elementa u svakom nizu
        MOV (AJ), (A)       ; (A), (B), (C) se prepisuju na odgovarajuce mesto
        ADD BJ, #B_START, J
        MOV (BJ), (B)
        ADD CJ, #C_START, J
        MOV (CJ), (C)

        BEQ T, 0, preskok   ; Nije trougao, J se ne pomera (elem. biva prepisan)
        ADD J, J, 1         ; Jeste trougao, J se pomera (elem. ostaje u nizu)
preskok:RTS
